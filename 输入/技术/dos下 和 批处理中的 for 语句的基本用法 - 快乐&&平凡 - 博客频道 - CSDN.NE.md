dos下 和 批处理中的 for 语句的基本用法 - 快乐&&平凡 - 博客频道 - CSDN.NET

#   [dos下 和 批处理中的 for 语句的基本用法](http://blog.csdn.net/wh_19910525/article/details/7912440)

.

  标签： [dos](http://www.csdn.net/tag/dos)[command](http://www.csdn.net/tag/command)[cmd](http://www.csdn.net/tag/cmd)[bbs](http://www.csdn.net/tag/bbs)[微软](http://www.csdn.net/tag/%e5%be%ae%e8%bd%af)

 2012-08-27 15:38  20633人阅读    [评论](http://blog.csdn.net/wh_19910525/article/details/7912440#comments)(8)    [收藏](dos下%20和%20批处理中的%20for%20语句的基本用法%20-%20快乐&&平凡%20-%20博客频道%20-%20CSDN.NE.md#)    [举报](http://blog.csdn.net/wh_19910525/article/details/7912440#report)

 .

 ![category_icon.jpg](../_resources/760c5ec8c68b26ded5d32a15a75b0d4b.jpg)  分类：

   windows相关*（22）*  ![arrow_triangle _down.jpg](../_resources/f4957b54c1e7e28871f863560acc9791.jpg)

 .

版权声明：本文为博主原创文章，未经博主允许不得转载。

for 语句的基本用法 ：

    最复杂的for 语句，也有其基本形态，它的模样是这样的：

 **  在cmd 窗口中**：for** %I** in (command1) do command2
   **在批处理文件中**：for** %%I **in (command1) do command2

   之所以要区分 cmd 窗口和批处理文件两种环境，是因为在这两种环境下，命令语句表现出来的行为虽然基本一样，但是在细节上还是稍有不同，最明显的一个差异就是：**在cmd 窗口中，for 之后的形式变量I 必须使用单百分号引用，即%I；而在批处理文件中，引用形式变量I 必须使用双百分号，即%%I**。为了方便起见，若不是特别强调，以下的讲解都以批处理文件环境为例。

   我们先来看一下for 语句的基本要素都有些什么：

   1、**for、in 和do** 是  for 语句的关键字，它们三个缺一不可；
   2、**%%I** 是for 语句中对形式变量的引用，就算它在do 后的语句中没有参与语句的执行，也是**必须出现**的；
   3、**in 之后，do 之前的括号不能省略**；
   4、command1 表示字符串或变量，command2 表示字符串、变量或命令语句；

   5、for /d  注意：for 后 可以 加 /f /r /l /d 这四个 参数，现在 先 介绍 /d 参数的 作用，一句话** /d 用于 搜索 目录 或 文件夹，但 不会搜索文件，也不搜索 子目录**；

   现在，你可能已经会写一个简单的for 语句了，比如：
在 一个 cmd.bat 文件里 写，写 如下 两条 命令：
for /d %%i in (*) do echo %%i
pause

[code1]
@echo off
for %%I in (bbs.bathome.cn) do echo %%I
pause

   保存为批处理文件并执行，将会在弹出的批处理窗口中看到这样的信息：

[result1]
bbs.bathome.cn
请按任意键继续...

  先来分析一下 for 语句的一些注意事项，之后，再让大家看看更为强大的for 语句实例。

   1、for 语句的形式变量 I，可以换成 26 个字母中的任意一个，这些**字母会区分大小写**，也就是说，%%I 和%%i 会被认为不是同一个变量；形式变量 I还可以换成其他的字符，但是，为了不与批处理中的%0～%9 这10 个形式变量发生冲突，请不要随意把%%I 替换为%%0  ～%%9 中的任意一个；

   2**、in 和do 之间的command1 表示的字符串或变量可以是一个，也可以是多个**，每一个字符串或变量，我们称之为一个元素，**每个元素之间，用空格键、跳格键、逗号、分号或等号分隔；

**
**
**

   3、for 语句依次提取 command1 中的每一个元素，把它的值赋予形式变量I,带到do 后的command2 中参与命令的执行；并且每次只提取一个元素，然后执行一次 do 后的命令语句，而无论这个元素是否被带到 command2 中参与了command2 的运行；当执行完一次do 后的语句之后，再提取command1 中的下一个元素，再执行一次 command2，如此循环，直到 command1 中的所有元素都已经被提取完毕，该for 语句才宣告执行结束；

   其中，第3 点是最为关键的，它描述了for 语句的执行过程，是for 语句的精髓所在，大家一定要牢记这一条，才能深刻理解更为复杂的for 流程。

   有了以上的基础，我们再来看一个例子，这个例子修改了code1 的部分内容，
结果将大不一样：

[code2]
@echo off
for %%I in (bbs,bathome,cn) do echo %%I
pause

   和code1 的执行结果相比，code2 的执行结果发生了如下变化：
   1、显示结果分成了3 行（不算最后一行中文提示）；
   2、每一行都从逗号处被切分；

   **如果把  bbs.bathome.cn  这个字符串中的点号换为 空格、跳格 或 等号，执行结果将和code2 的执行结果别无二致。**
** **
**以下这段代码的 功能是 ：检测当前硬盘都有哪些分区**

[code3]
@echo off
set str=c d e f g h i j k l m n o p q r s t u v w x y z
echo  当前硬盘的分区有：
for %%i in (%str%) do if exist %%i: echo %%i:
pause

   这段代码能检测硬盘都有哪些分区，包括 U 盘和移动硬盘的分区，但是，
当光驱中有盘的时候，也会被列出来，这是本代码的一个缺憾，在以后的讲解中，
我将向大家讲述如何消除这个瑕疵，敬请关注本系列的后续章节。

  ** 想知道当前目录下都有哪些文件，**请用下面的代码：

@echo off
for %%i in (*.*) do echo "%%i"
pause
 第二句 可以 用 for %%i in (*) do echo "%%i"  代替；

   想列出当前目录下所有的文本文件吗？请用下面的代码：

@echo off
for %%i in (*.txt) do echo "%%i"
pause

   想列出只用两个字符作为文件名的文本文件吗？请用下面的代码：

@echo off
for %%i in (??.txt) do echo "%%i"
pause
 ===============================
   题外话：

   1、列出当前目录下各种文件的方法，最简单的还是用dir 命令，但是，从以上代码中，各位可以加深对for 语句执行流程的理解（用到了通配符*和?）；
   2、注意：以上代码不能列出含有隐藏或系统属性的文件；

-----------------------------------------

**    三、文本解析显神威：for /f  用法详解 **

    前言

   for /f  是个十分强大的家伙。

   如果说，for 语句是批处理中最强大的语句的话，那么，for /f  就是精华中的精华。

   for  /f  的强大，和它拥有众多的开关密切相关。因为开关众多，所以用法复杂，本章将分成若干小节，为大家逐一介绍强大的  for /f  语句。

**    （一）  为解析文本而生：f or /f  的基本用法**

   所有的对象，无论是文件、窗体、还是控件，在所有的非机器语言看来，无外乎都是形如"c:\test.txt"、"CWnd"之类的文本信息；而所有的对象，具体的如ini

文件中的某条配置信息、注册表中的某个键值、[数据库](http://lib.csdn.net/base/mysql)中的某条记录„„都只有转化为具有一定格式的文本信息，方可被代码识别、操控。可以说，编程的很大

一部分工作，都是在绞尽脑汁想方设法如何提取这些文本信息。

   而提取文本信息，则是for /f 的拿手好戏：读取文件内容；提取某几行字符；截取某个字符片段；对提取到的内容再切分、打乱、杂糅„„只要你所能想到的花样，for /f  都会想方设法帮你办到，因为，for /f  就是被设计成专门用于解析文本的。

   先来看个例子。

   假如有个文本文件test.txt，内容如下：

[txt1]
论坛的目标是：不求最大，但求最好，做最实用的批处理论坛。
论坛地址：bbs.bathome.cn。
这里是：新手晋级的福地，高手论剑的天堂。

   那么，将如下代码保存为test.cmd，并放在test.txt 同一目录下运行，将会在屏幕上原样显示test.txt 的内容：

[code4]
@echo off
for /f %%i in (test.txt) do echo %%i
pause

   这段代码，主要是让你树立这样一种观念：**读取文本文件的内容，请使用  for /f  语句！**

   进阶话题：for /f  语句是把整个test.txt 一次性显示出来的？

   在这段代码中，虽然执行结果是把 test.txt 中的所有内容都显示出来了，貌似  for /f  语句是把整个test.txt 一次性显示到屏幕上，实际上并非如此。

   无论for 语句做何种变化，它的执行过程仍然遵循基本的for 流程：依次处理每个元素，直到所有的元素都被处理为止。只不过在for /f 语句中，这里的元素是指文件中的每一行，也就是说，**for /f  语句是以行为单位处理文本文件的**。这是一条极为重要的规则，在上一章中也强调过它的重要性，希望在接下来的学习过程中，你能时刻牢记这一原则，那么，很多问题将会迎刃而解。以下是验证这一说法的演示代码（在[code4]的基础上添加了&pause 语句）：

[code5]
@echo off
for /f %%i in (test.txt) do echo %%i&pause
pause

    （二） ** 切分字符串的利器：delims=**

   也许你对[code4]这段代码不屑一顾：不就是把test.txt 的内容显示出来了么？
好像用处不大啊。

   好吧，我们来玩个魔术。

   还是[txt1]这段文本，把[code4]改造一下：

[code6]
@echo off
for /f "delims=，" %%i in (test.txt) do echo %%i
pause

   再次运行test.cmd，看到什么变化了吗？！

[result2]
论坛的目标是：不求最大
论坛地址：bbs.bathome.cn。
这里是：新手晋级的福地
请按任意键继续...

   结果，你惊奇地发现，每行第一个逗号之后的所有内容都不见了（如果有不存在逗号的行，则保留原样），也就说，你成功地提取到了每行第一个逗号之前的所有内容！

   试想一下，这段代码会有什么用呢？

   如果别人给了你一个软件清单，每行都是"英文软件名（逗号）中文软件名"的格式，而你却只想保留英文名的时候，这段代码将是多么有用啊！再假设，有这么一个IP 文件，第一列是数字格式的IP 地址，第二列是具体的空间地址，列与列之间用逗号分隔，而你想提取其中数字格式的 IP，呵呵，我不说你也知道该怎么办了吧？

   要是文本内容不是以逗号分隔，而是以其他符号分隔，那么，把"delims=,"的逗号换成相应的符号就可以了。

   在这里，我们引入了一个新的开关："delims=，"，它的含义是：以逗号作为被处理的字符串的分隔符号。

   在批处理中，指定分隔符号的方法是：添加一个形如  "delims=符号列表"  的开关，这样，被处理的每行字符串都会被符号列表中罗列出来的符号切分开来。

   需要注意的是：如果没有指定"delims=符号列表"这个开关，那么，for /f  语句默认以空格键或跳格键作为分隔符号。请把[txt1]中不同位置上的标点符号改为空格或跳格，再运行[code4]试试。

=============================================
   进阶话题：如果我要指定的符号不止一个，该怎么办？

   在上面的讲解中，我提到了指定分隔符号的方法：添加一个形如"delims=符
号列表"的开关。不知道你注意到没有，我的说法是"符号列表"而非"符号"，这是
大有讲究的，因为，你可以一次性指定多个分隔符号！

10

   还是以[txt1]为例，把[code6]再改造一下：

[code7]
@echo off
for /f "delims=.，" %%i in (test.txt) do echo %%i
pause

   结果显示：

[result3]
论坛的目标是：不求最大
论坛地址：bbs
这里是：新手晋级的福地
请按任意键继续...

   这样，第一个点号或第一个逗号之前的内容都被提取出来了。

   [code7]的执行过程是：逐行读取 test.txt 中的内容，以点号和逗号切分每一
行的内容（不存在点号和逗号的行，则不再切分，为了描述的方便，我们把被点
号或逗号切分的一个一个的字符串片段，称之为节），然后，for /f  会提取第一节
的内容作为最终结果，显示在屏幕上。需要注意的是，在这里，所有行的字符串
被切分成了两个以上的节，但是，[code7]的代码只会提取第一节字符串的内容，
因为  for /f  语句默认只提取第一节的符串。

    （三）  定点提取：tokens=

   上一节在讲解  delims=  的时候，我一再强调  for  /f  默认只能提取到第一节
的内容，现在我们来思考一个问题：如果我要提取的内容不在第一节上，那怎么
办？

   这回，就该轮到  tokens=  出马了。

   tokens=  后面一般跟的是数字，如  tokens=2，也可以跟多个，但是每个数字
之间用逗号分隔，如  tokens=3,5,8，它们的含义分别是：提取第2 节字符串、提
取第3、第5 和第8 节字符串。注意，这里所说的“节”，是由  delims=  这一开
关划分的，它的内容并不是一成不变的。

   下面来看一个例子：

11
[txt2]
尺有所短，寸有所长，学好批处理没商量，考虑问题复杂化，解决问题简洁化。

   对[txt2]这段文本，假设它们保存在文件 test.txt 中，如果我想提取“学好批
处理没商量”这句话，该如何写代码呢？

   我们稍微观察一下[txt2]就会发现，如果以逗号作为切分符号，就正好可以
把“学好批处理没商量”化为单独的一“节”，结合上一节的讲解，我们知道，
"delims=，"  这个开关是不可缺少的，而要提取的内容在以逗号切分的第3 节上，
那么，tokens=  后面的数字就应该是3 了，最终的代码如下：

[code8]
@echo off
for /f "delims=，  tokens=3" %%i in (test.txt) do echo %%i
pause

   如果我们现在要提取的不只一个“节”，而是多个，那又怎么办呢？比如，
要提取以逗号切分的第2 节和第5 节字符串，是写成这样吗？

[code9]
@echo off
for /f "delims=，  tokens=2,5" %%i in (test.txt) do echo %%i
pause

   运行批处理后发现，执行结果只显示了第2 节的内容。

   原来，echo  后面的  %%i  只接收到了  tokens=2,5  中第一个数值2 所代表的
那个字符串，而第二个数值5 所代表的字符串因为没有变量来接收，所以就无法
在执行结果中显示出来了。

   那么，要如何接收  tokens=  后面多个数值所指代的内容呢？

   for /f  语句对这种情况做如下规定：

   如果  tokens=  后面指定了多个数字，如果形式变量为%%i，那么，第一个
数字指代的内容用第一个形式变量%%i 来接收，第二个数字指代的内容用第二
个形式变量%%j 来接收，第三个数字指代的内容用第三个形式变量%%k 来接
收„„第N 个数字指代的内容用第N 个形式变量来接收，其中，形式变量遵循
字母的排序，第 N 个形式变量具体是什么符号，由第一个形式变量来决定：如
果第一个形式变量是%%i，那么，第二个形式变量就是%%j；如果第一个形式
变量用的是%%x，那么，第二个形式变量就是%%y。

   现在回头去看[code9]，你应该知道如何修改才能满足题目的要求了吧？修改

12
结果如下：

[code10]
@echo off
for /f "delims=，  tokens=2,5" %%i in (test.txt) do echo %%i %%j
pause

   如果有这样一个要求：显示[txt2]中的内容，但是逗号要替换成空格，如何
编写代码？

   结合上面所学的内容，稍加思索，你可能很快就得出了答案：

[code11]
@echo off
for  /f  "delims= ，   tokens=1,2,3,4,5"  %%i  in  (test.txt)  do
echo %%i %%j %%k %%l %%m
pause

   写完之后，你可能意识到这样一个问题：假如要提取的“节”数不是5，而
是10，或者20，或者更多，难道我也得从1 写到10、20 或者更多吗？有没有更
简洁的写法呢？

   答案是有的，那就是：如果要提取的内容是连续的多“节”的话，那么，连
续的数字可以只写最小值和最大值，中间用短横连接起来即可，比如
tokens=1,2,3,4,5  可以简写为  tokens=1-5  。

   还可以把这个表达式写得更复杂一点：tokens=1,2-5 ，tokens=1-3,4,5 ，
tokens=1-4,5„„怎么方便就怎么写吧。

   大家可能还看到一种比较怪异的写法：

[code12]
@echo off
for /f "delims=，  tokens=1,*" %%i in (test.txt) do echo %%i %%j
pause

   结果，第一个逗号不见了，取代它的是一个空格符号，其余部分保持不变。

   其中奥妙就在这个星号上面。

    tokens=后面所接的星号具备这样的功能：字符串从左往右被切分成紧跟在*
之前的数值所表示的节数之后，字符串的其余部分保持不变，整体被*所表示的
一个变量接收。

13
   理论讲解是比较枯燥的，特别是为了严密起见，还使用了很多限定性的修饰
词，导致句子很长，增加了理解的难度，我们还是结合[code12]来讲解一下吧。

   [txt2]  的内容被切分，切分符号为逗号，当切分完第一节之后，切分动作不
再继续下去，因为  tokens=1,*  中，星号前面紧跟的是数字 1；第一节字符串被
切分完之后，其余部分字符串不做任何切分，整体作为第二节字符串，这样，[txt2]
就被切分成了两节，分别被变量%%i 和变量%%j 接收。

   以上几种切分方式可以结合在一起使用。不知道下面这段代码的含义你是否
看得懂，如果看不懂的话，那就运行一下代码，然后反复揣摩，你一定会更加深
刻地理解本节所讲解的内容的：

[code13]
@echo off
for /f "delims=，  tokens=1,3-4,*" %%i in (test.txt) do echo %%i %%j %%k %%l
pause

    （四）  跳过无关内容，直奔主题：skip=n

   很多时候，有用的信息并不是贯穿文本内容的始终，而是位于第 N 行之后
的行内，为了提高文本处理的效率，或者不受多余信息的干扰，for  /f  允许你跳
过这些无用的行，直接从第N+1 行开始处理，这个时候，就需要使用参数  skip=n，
其中，n 是一个正整数，表示要跳过的行数。例如：

[code14]
@echo off
for /f "skip=2" %%i in (test.txt) do echo %%i
pause

   这段代码将跳过头两行内容，从第3 行起显示test.txt 中的信息。

    （五）  忽略以指定字符打头的行：eol=

   在cmd 窗口中敲入：for /?，相关的解释为：

[quote]
eol=c                      -  指一个行注释字符的结尾(就一个)
[/quote]

14
[quote]
FOR /F "eol=; tokens=2,3* delims=, " %i in (myfile.txt) do @echo %i %j %k

     会分析  myfile.txt  中的每一行，忽略以分号打头的那些行„„
[/quote]

   第一条解释狗屁不通，颇为费解：行注释字符的结尾是什么意思？“(就一
个)”怎么回事？结合第二条解释，才知道eol 有忽略指定行的功能。但是，这两
条解释是互相矛盾的：到底是忽略以指定字符打头的行，还是忽略以指定字符结
尾的行？

   实践是检验真理的唯一标准，还是用代码来检验一下eol 的作用吧：
[code15]
@echo off
for /f "eol=;" %%i in (test.txt) do echo %%i
pause

   结果，那些以分号打头的行没有显示出来。

   由此可见，第二条解释是正确的，eol=  的准确含义是：忽略以指定字符打
头的行。而第一条的“结尾”纯属微软在信口开河。

   那么，“(就一个)”又作何解释呢？

   试试这个代码：
[code16]
@echo off
for /f "eol=,;" %%i in (test.txt) do echo %%i
pause

   此时，屏幕上出现  此时不应有  ;"。  的报错信息。可见，在指定字符的时
候，只能指定1 个——在很多时候，我对这样的设计颇有微词而又无可奈何：为
什么只能指定1 个而不是多个？要忽略多个还得又是 if 又是 findstr 加管道来多
次过滤，那效率实在太低下了——能用到的功能基本上都提供，但是却又做不到
更好，批处理，你的功能为什么那么弱？

   不知道大家注意到没有，如果 test.txt 中有以分号打头的行，那么，这些行
在代码[code14]的执行结果中将凭空消失。

   原来，for /f  语句是默认忽略以分号打头的行内容的，正如它默认以空格键
或跳格键作为字符串的切分字符一样。

   很多时候，我们可以充分利用这个特点，比如，在设计即将用for 读取的配
置文件的时候，可以在注释文字的行首加上分号，例如在编写病毒文件查杀代码

15
的时候，可以通过 for 语句来读取病毒文件列表，那么，病毒文件列表.ini 这个
配置文件可以这样写：

;以下是常见的病毒文件，请见一个杀一个^_^
;copyleft:没有
qq.exe
msn.exe
iexplore.exe

   如果要取消这个默认设置，可选择的办法是：

   1、为eol=指定另外一个字符；
   2、使用  for /f "eol="  语句，也就是说，强制指定字符为空，就像对付delims=
一样。

    （六）如何决定该使用  f or  /f  的哪种句式？(兼谈
usebackq 的使用)

   for /f %%i in („„) do („„)  语句有好几种变形语句，不同之处在于第一个
括号里的内容：有的是用单引号括起来，有的是用双引号包住，有的不用任何符
号包裹，具体格式为：

   1、for /f %%i in (文件名) do („„)
   2、for /f %%i in ('命令语句') do („„)
   3、for /f %%i in ("字符串") do („„)

   看到这里，我想很多人可能已经开始犯了迷糊了：如果要解决一个具体问题，
面对这么多的选择，如何决定该使用哪一条呢？

   实际上，当我在上面罗列这些语句的时候，已经有所提示了，不知道你是否
注意到了。

   如果你一时无法参透其中奥妙，那也无妨，请听我一一道来便是。

   1、当你希望读取文本文件中的内容的话，第一个括号中不用任何符号包裹，
应该使用的是第1 条语句；例如：你想显示test.txt 中的内容，那么，就使用  for
/f %%i in (test.txt) do echo %%i；
   2、当你读取的是命令语句执行结果中的内容的话，第一个括号中的命令语
句必须使用单引号包裹，应该使用的是第2 条语句；例如：你想显示当前目录下

16
文件名中含有test 字符串的文本文件的时候，应该使用  for /f %%i in ('dir /a-d
/b *test*.txt') do echo %%i  这样的语句；
   3、当你要处理的是一个字符串的时候，第一个括号中的内容必须用双引号
括起来，应该是用的是第 3 条语句；例如：当你想把 bbs.bathome.cn 这串字符
中的点号换为短横线并显示出来的话，可以使用  for  /f  "delims=.
tokens=1-3"  %%i  in  ("bbs.bathome.cn")  do  echo  %%i-%%j-%%k  这样的语
句。

   很显然，第一个括号里是否需要用符号包裹起来，以及使用什么样的符号包
裹，取决于要处理的对象属于什么类型：如果是文件，则无需包裹；如果是命令
语句，则用单引号包裹；如果是字符串，则使用双引号括起来。

   当然，事情并不是绝对如此，如果细心的你想到了批处理中难缠的特殊字符，
你肯定会头大如斗。

   或许你头脑中灵光一闪，已经想到了一个十分头痛的问题：在第1 条语句中，
如果文件名中含有空格或&，该怎么办？

   照旧吗？

   拿个叫  test 1.txt  的文件来试试。

   你很快写好了代码，新建文件-->码字-->保存为批处理，前后费时不到 1 分
钟：
[code17]
@echo off
for /f %%i in (test 1.txt) do echo %%i
pause

   你兴冲冲地双击批处理，运行后，屏幕上出现了可耻的报错信息：系统找不
到文件  test  。

   当你把  test 1.txt  换成  test&1.txt  后，更怪异的事情发生了：CMD 窗口在你
眼前一闪而过，然后，优雅地消失了。

   你可能觉得自己的代码写错了某些符号，你再仔细的检查了一次，确认没有
笔误，然后，你再次双击批处理，结果问题照旧；你开始怀疑其他程序对它可能
有影响，于是关掉其他窗口，再运行了一次，问题依旧；你不服气地连续运行了
好几次，还是同样的结果。

   怪哉！

   你一拍大腿，猛然想起了一件事：当路径中含有特殊字符的时候，应该使用
引号把路径括起来。对，就是它了！

17

   但是，当你把代码写出来之后，你很快就焉了：for /f %%i in ("test 1.txt") do
echo  %%i，这不就是上面提到的第3 条  for  /f  命令的格式吗？批处理会把  test
1.txt  这个文件名识别为字符串啊！

   你百无聊赖地在CMD 窗口中输入  for /?  ，并重重地敲下了回车，漫无目的
地在帮助信息中寻找，希望能找到点什么。

   结果还真让你到了点什么。

   你看到了这样的描述：
         usebackq                -  指定新语法已在下类情况中使用:
                           在作为命令执行一个后引号的字符串并且一个单
                           引号字符为文字字符串命令并允许在  filenameset
                            中使用双引号扩起文件名称。

   但是，通读一遍之后，你却如坠五里雾中，不知所云。

   还好，下面有个例子，并配有简单的说明：

       FOR /F "usebackq delims==" %i IN (`set`) DO @echo %i

     会枚举当前环境中的环境变量名称。

   你仔细对比了 for  /f 语句使用usebackq 和不使用usebackq 时在写法上的差
别，很快就找到了答案：当使用了 usebackq 之后，如果第一个括号中是一条命
令语句，那么，就要把单引号'改成后引号`（键盘左上角esc 键下面的那个按键，
与~在同一键位上）。

   回过头去再看那段关于 usebackq 的描述，字斟句酌，反复揣摩，终于被你
破译了天机：usebackq  是一个增强型参数，当使用了这个参数之后，原来的for
语句中第一个括号内的写法要做如下变动：如果第一个括号里的对象是一条命
令语句的话，原来的单引号'要改为后引号`；如果第一个括号里的对象是字符串
的话，原来的双引号"要改为单引号'；如果第一个括号里的对象是文件名的话，
要用双引号"括起来。

   验证一下，把[code17]改写成如下代码：
[code18]
@echo off
for /f "usebackq" %%i in ("test 1.txt") do echo %%i
pause

   [测试](http://lib.csdn.net/base/softwaretest)通过！

18
   此时，你很可能会仰天长叹：Shit，微软这该死的机器翻译！

   至于把[code17]代码中的空格换成&后，CMD 窗口会直接退出，那是因为&
是复合语句的连接符，CMD 在预处理的时候，会优先把&前后两部分作为两条
语句来解析，而不是大家想象中的一条完整的for 语句，从而产生了严重的语法
错误。因为牵涉到预处理机制问题，不属于本节要讨论的内容，在此不做详细讲
解。

   这个时候，我们会吃惊地发现，区区一条for 语句，竟然有多达6 种句型：

   1、for /f %%i in (文件名) do („„)
   2、for /f %%i in ('命令语句') do („„)
   3、for /f %%i in ("字符串") do („„)
   4、for /f "usebackq" %%i in ("文件名") do („„)
   5、for /f "usebackq" %%i in (`命令语句`) do („„)
   6、for /f "usebackq" %%i in ('字符串') do („„)

   其中，4、5、6 由1、2、3 发展而来，他们有这样的对应关系：1-->4、2-->5、
3-->6。

   好在后3 种情形并不常用，所以，牢牢掌握好前三种句型的适用情形就可以
了，否则，要在这么多句型中确定选择哪一条语句来使用，还真有点让人头脑发
懵。

   至于  for /f  为什么要增加usebacq 参数，我只为第4 条语句找到了合理的解
释：为了兼容文件名中所带的空格或&。它在第 5、6 条语句中为什么还有存在
的必要，我也不是很明白，这有待于各位去慢慢发现。

[(L)](http://blog.csdn.net/wh_19910525/article/details/7912440#)[(L)](http://blog.csdn.net/wh_19910525/article/details/7912440#)[(L)](http://blog.csdn.net/wh_19910525/article/details/7912440#)[(L)](http://blog.csdn.net/wh_19910525/article/details/7912440#)[(L)](http://blog.csdn.net/wh_19910525/article/details/7912440#)[(L)](http://blog.csdn.net/wh_19910525/article/details/7912440#).

顶

1

踩

0

 .

[dos下 和 批处理中的 for 语句的基本用法 - 快乐&&平凡 - 博客频道 - CSDN.NE](dos下%20和%20批处理中的%20for%20语句的基本用法%20-%20快乐&&平凡%20-%20博客频道%20-%20CSDN.NE.md#)

 [dos下 和 批处理中的 for 语句的基本用法 - 快乐&&平凡 - 博客频道 - CSDN.NE](dos下%20和%20批处理中的%20for%20语句的基本用法%20-%20快乐&&平凡%20-%20博客频道%20-%20CSDN.NE.md#)

- 上一篇[dos的 if语句](http://blog.csdn.net/wh_19910525/article/details/7912123)

- 下一篇[APK文件简介 及 解包 打包 工具 介绍](http://blog.csdn.net/wh_19910525/article/details/7914714)

 .

#### 我的同类文章

   windows相关*（22）*

- *•*[在Windows 7 上大家Android studio环境](http://blog.csdn.net/wh_19910525/article/details/55509490)2017-02-17*阅读***34**

- *•*[Eclipse导入Android项目的正确方法](http://blog.csdn.net/wh_19910525/article/details/17501887)2013-12-23*阅读***944**

- *•*[windows下 git 的 颜色 和 命令别名 的配置](http://blog.csdn.net/wh_19910525/article/details/8317096)2012-12-19*阅读***1528**

- *•*[Imagemagick 对图片 大小 和 格式的 调整](http://blog.csdn.net/wh_19910525/article/details/8182938)2012-11-14*阅读***5034**

- *•*[从零 使用vc](http://blog.csdn.net/wh_19910525/article/details/8180931)2012-11-13*阅读***542**

- *•*[Git的Windows版本Msysgit的中文乱码解决](http://blog.csdn.net/wh_19910525/article/details/8155621)2012-11-06*阅读***2960**

- *•*[新建 一个android工程，res/layout 下的xml布局文件无法预览](http://blog.csdn.net/wh_19910525/article/details/17502749)2013-12-23*阅读***5441**

- *•*[在source insight中 添加新的文件类型](http://blog.csdn.net/wh_19910525/article/details/9626531)2013-07-30*阅读***1466**

- *•*[安装cygwin](http://blog.csdn.net/wh_19910525/article/details/8256159)2012-12-04*阅读***2393**

- *•*[C++之文件IO操作流](http://blog.csdn.net/wh_19910525/article/details/8181033)2012-11-14*阅读***2850**

- *•*[VC6.0的 错误解决办法 -- 小结](http://blog.csdn.net/wh_19910525/article/details/8176448)2012-11-12*阅读***561**

 [更多文章](http://blog.csdn.net/wh_19910525/article/category/1205665)